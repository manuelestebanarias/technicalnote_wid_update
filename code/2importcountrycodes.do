// -------------------------------------------------------------------------- //
// 2.   Import countries and region codes
// -------------------------------------------------------------------------- //


// ---------- 1. import core countries
import excel "$codes_dictionary", sheet("Country_List") cellrange(A2:K219) firstrow clear

drop Alpha3  ISONumeric region region2 
rename (Alpha2 TitleName ShortName) (country titlename shortname)


ds country titlename shortname TH, not

* Name generically the regions                
local cnt = 1
foreach r in `r(varlist)' {
    rename `r' region`cnt'
    local cnt = `cnt' + 1
}

gen corecountry=1
drop if country=="WO"

* clean repeated subdivision denominations
*replace region3="" if region2==region3

order country titlename shortname

* Call the region's names
ds country titlename shortname corecountry TH, not
preserve
	* Generate a table with name-Alpha2 codes equivalences 
	import excel "$codes_dictionary", sheet("Region_List") cellrange(A2:B22) firstrow clear	
	replace Alpha2 = substr(Alpha2,1,2)
	replace ShortName = substr(ShortName, 1, length(ShortName) - 6) if regexm(ShortName, " \(MER\)$")
	foreach r in `r(varlist)'{
		gen `r' = ShortName
	}
	drop ShortName
	rename (Alpha2) (code)

	foreach v in region1 region2 region3 {
		replace `v' = "South & South-East Asia"       if code=="XS"
		replace `v' = "Other South & South-East Asia" if code=="OI"
	}
	tempfile regions_list
	save "`regions_list'"
restore

ds country titlename shortname corecountry TH, not
* Bring the table generated and replace the names by alpha2 codes
foreach r in `r(varlist)'{
	merge m:1 `r' using "`regions_list'", nogenerate keepusing(code) keep(master matched)
	drop `r'
	rename code `r'
}


// ---------- 2. import historical countries
preserve
	import excel "$codes_dictionary", sheet("Historical_Country_List") cellrange(A2:C7) firstrow clear
	rename (Alpha2 ShortName TitleName) (country shortname titlename)
	tempfile historical
	save   "`historical'"
restore
// ---------- 3. import Regions
preserve
	import excel "$codes_dictionary", sheet("Region_List") cellrange(A2:B22) firstrow clear
	rename (Alpha2 ShortName) (country shortname)
	drop if country=="WO"
	
	replace country = substr(country,1,2)
	replace shortname = substr(shortname, 1, length(shortname) - 6) if regexm(shortname, " \(MER\)$")
	replace shortname = "South & South-East Asia"       if country=="XS"
	replace shortname = "Other South & South-East Asia" if country=="OI"
	
	tempfile regions
	save   "`regions'"
restore
// ---------- 4. import the sub country territories
preserve
	import excel "$codes_dictionary", sheet("Sub_Country_List") cellrange(A2:C63) firstrow clear
	rename (Alpha2 ShortName TitleName) (country shortname titlename)
	tempfile country_subregion
	save   "`country_subregion'"
restore
// ---------- 5. import Other jurisdiction
preserve
	import excel "$codes_dictionary", sheet("Other_Juridisdictions_List") cellrange(A2:C30) firstrow clear
	rename (Alpha2 ShortName TitleName) (country shortname titlename)
	tempfile other_country
	save   "`other_country'"
restore

// ---------- 6. Append all lists
append using "`historical'"
append using "`regions'"
append using "`country_subregion'"
append using "`other_country'"

*Clean-up
replace titlename=shortname if missing(titlename)
sort country

// -------------------------------------------------------------------------- //
// Core Countries only
// -------------------------------------------------------------------------- //
preserve
	keep if corecountry == 1
	drop if country=="WO"
	label data "Generated by import-country-codes.do"
	save "$work_data/import-core-country-codes-output.dta", replace
restore

// -------------------------------------------------------------------------- //
// Core Countries-year
// -------------------------------------------------------------------------- //
preserve
	keep if corecountry == 1
	drop if country=="WO"
	local n_years = $pastyear - 1970 + 1
	expand `n_years'
	sort country 
	bysort country: gen year = 1970 + _n - 1

	
	label data "Generated by import-country-codes.do"
	save "$work_data/import-core-country-codes-year-output.dta", replace
restore
// -------------------------------------------------------------------------- //
// Countries only
// -------------------------------------------------------------------------- //

preserve
	drop if inrange(country, "QB", "QZ") | country == "WO"  | inrange(country, "OA", "OJ") ///
	| inrange(country, "XA", "XF") | inrange(country, "XL", "XS")
	label data "Generated by import-country-codes.do"
	save "$work_data/import-country-codes-output.dta", replace
restore

// -------------------------------------------------------------------------- //
// Regions (PPP)
// -------------------------------------------------------------------------- //

preserve
	keep if inrange(country, "QB", "QZ") | country == "WO"  | inrange(country, "OA","OL") ///
	| inrange(country, "XA", "XF") | inrange(country, "XL", "XS")
	keep country titlename shortname
	
	generate matchname = shortname
	
	rename country region1
	gen region2=region1
	gen region3=region1
	
*	replace titlename = titlename + " (at purchasing power parity)"
*	replace shortname = shortname + " (at purchasing power parity)"
	gen      order = 1 		if region1=="QL"
	replace  order = 2 		if region1=="QE"
	replace  order = 3 		if region1=="XL"
	replace  order = 4 		if region1=="XN"
	replace  order = 5 		if region1=="XB"
	replace  order = 6 		if region1=="XR"
	replace  order = 7 		if region1=="XS"
	replace  order = 8 		if region1=="XF"
	replace  order = 9 		if region1=="WO"
	
	label data "Generated by import-country-codes.do"
	save "$work_data/import-region-codes-output.dta", replace
restore

// -------------------------------------------------------------------------- //
// Regions (MER)
// -------------------------------------------------------------------------- //
/*
preserve
	keep if inrange(country, "QB", "QZ") | country == "WO"  | inrange(country, "OA", "OJ") ///
	| inrange(country, "XA", "XF") | inrange(country, "XL", "XS")
	keep country titlename shortname
	
	replace country = country + "-MER"
	replace titlename = titlename + " (at market exchange rate)"
	replace shortname = shortname + " (at market exchange rate)"
	
	label data "Generated by import-country-codes.do"
	save "$work_data/import-region-codes-mer-output.dta", replace
restore
*/
